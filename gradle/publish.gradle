publishing {
    publications {
        kotlinMultiplatform {
            artifactId = module.name
            groupId = "com.splendo.kaluga"
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "splendo"
            name "Splendo BV"
        }
    }
    scm {
        url "https://github.com/splendo/kaluga"
    }
}

// See: https://github.com/bintray/bintray-examples/blob/master/gradle-bintray-plugin-examples/publications-example/build.gradle
bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    dryRun = project.hasProperty('bintrayDryRun') ? project.property('bintrayDryRun') : true
    publish = false

    pkg {
        repo = 'com.splendo.kaluga'
        name = module.name
        userOrg = 'kaluga'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/splendo/kaluga'
    }
}

bintrayUpload.doFirst {
    publications = publishing.publications
}

afterEvaluate {
    project.publishing.publications.forEach { publication ->
        publication.pom.withXml {
            def root = asNode()
            root.appendNode('name', project.name)
            root.appendNode('description', 'Collection of Kotlin Flow based libraries')
            root.appendNode('url', 'https://github.com/splendo/kaluga')
            root.children().last() + pomConfig
        }
    }
}
