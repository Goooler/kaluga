pluginManagement {
    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == "kotlin-multiplatform") {
                useModule("org.jetbrains.kotlin:kotlin-gradle-plugin:${requested.version}")
            }
            if (requested.id.id == "com.android.library") {
                useModule("com.android.tools.build:gradle:${requested.version}")
            }
            if (requested.id.id == "com.android.application") {
                useModule("com.android.tools.build:gradle:${requested.version}")
            }
        }
    }

    repositories {
        google()
        jcenter()

        // uncomment this for using experimental versions
        //maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
        //maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
}

rootProject.name = 'SplendoMPP'

// Version
gradle.ext {
    kotlin_version = '1.3.50' // when changing this also change the build.gradle multiplatform plugin version
    kotlinx_corountines_version = '1.3.0'
    library_version = '0.0.2'
    android_version = 27
    android_target_version = 29
}

gradle.ext.one_ios_sourceset = ext.properties.containsKey("android.injected.invoked.from.ide") && !gradle.startParameter.taskNames.any { it.contains("publish") || it.contains("Test")}

println "Single iOS sourceset: $gradle.ext.one_ios_sourceset"
String explain = gradle.ext.one_ios_sourceset ? """    
    This is done when running from Android Studio, in order to work around https://youtrack.jetbrains.net/issue/KT-27678. 
    If the build is not from the IDE or the task set includes any task with the word "publish" or "Test" in it, this is not used.
    """ : """
    Running with all sourcesets (arm32, arm64) included, because we are outside of the IDE or publishing/testing.
    """
println explain.stripIndent().trim()

enableFeaturePreview('GRADLE_METADATA')

include ':logging'
include ':Components'

include ':example'
include ':example:android'
